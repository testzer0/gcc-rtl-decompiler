Program         :=      FuncBodyList                ;
FuncBodyList    :=      /* empty */                             
                |       FuncBodyList FuncBody       ;
FuncBody        :=      T_FunBegin StmtList         ;
StmtList        :=      /* empty */                            
                |       StmtList Stmt               ;
Stmt            :=      "(" Note ")" 
                |       "(" Barrier ")"
                |       "(" Insn ")"
                |       "(" JumpInsn ")"
                |       "(" Call ")"                ;
Note            :=       T_Note T_IntConstant T_IntConstant T_IntConstant     
                |        T_Note T_IntConstant T_IntConstant T_IntConstant T_IntConstant ;
Barrier         :=       T_Barrier T_IntConstant T_IntConstant T_IntConstant ;
Integer         :=       T_IntConstant 
                         "-" T_IntConstant           ;
Insn            :=       T_Insn T_IntConstant T_IntConstant T_IntConstant "(" MainCmd ")" \  
                          T_StringConstant ":" T_IntConstant Integer "(" T_Nil ")" ;
MainCmd         :=      PlainCmd 
                |       T_Parallel CmdList T_EndPara ;
CmdList         :=      /* empty */
                |       SingleCmd CmdList           ;
SingleCmd       :=      "(" PlainCmd ")"            ;
PlainCmd        :=      ClobberCmd | SetCmd | UseCmd ;
ClobberCmd      :=      T_Clobber "(" T_Reg T_CCType T_IntConstant T_Flags ")" ;
SetCmd          :=      T_Set "(" Operand ")" "(" Operand ")" ;
Operand         :=      T_ConstInt Integer 
                |       LocInfo ":" TypeInfo Expr   
                |       T_SiExtend ":" TypeInfo  "(" Operand ")" ;
LocInfo         :=      MemType Flags               ;
MemType         :=      T_Mem | T_Reg | T_SymbolRef ;
Flags           :=      /* empty */ 
                |       Flags Flag                  ;
Flag            :=      T_IFlag | T_VFlag | T_FFlag | T_CFlag ;
TypeInfo        :=      T_SIType | T_DIType | T_QIType | T_CCType | T_CCZType | T_CCGCType ;
Expr            :=      Integer 
                |       "(" PlusExpr ")"
                |       "(" MinusExpr ")"
                |       "(" MultExpr  ")"
                |       "(" AshiftExpr ")"
                |       "(" SubregExpr ")"          
                |       "(" CompareExpr ")"
PlusExpr        :=      T_Plus TypeInfo "(" Operand ")" "(" Operand ")" ;
MinusExpr       :=      T_Minus TypeInfo "(" Operand ")" "(" Operand ")" ;
MultExpr        :=      T_Mult TypeInfo "(" Operand ")" "(" Operand ")" ;
AshiftExpr      :=      T_Ashift TypeInfo "(" Operand ")" "(" Operand ")" ;
SubregExpr      :=      T_SubReg TypeInfo "(" Operand ")" Integer ;
CompareExpr     :=      T_Compare TypeInfo "(" Operand ")" "(" Operand ")" ;
JumpInsn        :=      T_JumpInsn T_IntConstant T_IntConstant T_IntConstant "(" T_Set \
                          "(" T_Pc ")" "(" Dest ")" ")" T_StringConstant ":" T_IntConstant Integer "(" T_Nil ")" ;
Dest            :=      T_LabelRef T_IntConstant 
                |       T_IfThenElse "(" Comparison ")" "(" InDest ")" "(" InDest ")" ;
Comparison      :=      Condition "(" Operand ")" "(" Operand ")" ;
Condition       :=      T_Lt | T_Gt | T_Le | T_Ge | T_Eq | T_Ne ;
InDest          :=      T_LabelRef T_IntConstant 
                |       T_Pc ;
Call            :=      RetCall | NoRetCall  ;
RetCall         :=      T_CallInsn T_IntConstant T_IntConstant T_IntConstant T_IntConstant "(" SetInternal ")" \
                         T_StringConstant ":" T_IntConstant Integer "(" T_Nil ")" "(" Junk ")" ;
Junk            :=      T_ExprList "(" T_Use "(" T_Reg ":" TypeInfo T_IntConstant ")" ")" "(" T_Nil ")" ;
SetInternal     :=      T_Set "(" T_Reg ":" TypeInfo T_IntConstant ")" "(" CallInternal ")" ;
NoRetCall       :=      T_CallInsn "(" CallInternal ")" T_StringConstant ":" T_IntConstant Integer \
                          "(" T_Nil ")" "(" Junk ")" ;
CallInternal    :=      T_Call "(" T_Mem ":" T_QIType "(" T_SymbolRef ":" T_DIType \
                          "(" T_StringConstant ")" ")" ")" "(" T_ConstInt Integer ")"
